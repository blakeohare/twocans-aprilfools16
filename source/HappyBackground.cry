import GFX;
import Math;
import Random;


class HappyBackground {
	
	field counter = 0;
	field sprites = [SpriteType.CAT] * 3 + [SpriteType.BIRD] * 3 + [SpriteType.TREE] * 4;
	
	constructor() {
		this.sprites.shuffle();
	}
	
	function update(scene) {
		this.counter++;
		
		if (this.counter > 1 && this.counter % FPS == 0 && this.sprites.length > 0) {
			spriteType = this.sprites[0];
			this.sprites = this.sprites[1:];
			mode = null;
			switch (spriteType) {
				case SpriteType.CAT:
					x = Random.randomInt(100, SCREEN_WIDTH - 100);
					y = GROUND_Y;
					modes = ['wait', 'left', 'right'];
					modes.shuffle();
					mode = modes[0];
					break;
				case SpriteType.TREE:
					x = Random.randomInt(0, SCREEN_WIDTH);
					y = GROUND_Y;
					break;
				case SpriteType.BIRD:
					x = Random.randomInt(200, SCREEN_WIDTH - 200);
					while (true) {
						y = Random.randomInt(50, GROUND_Y - 150);
						if (abs(y - LETTERS_Y) > 40) {
							break;
						}
					}
					mode = Random.randomBool() ? 'left' : 'right';
					break;
			}
			
			sprite = new Sprite(spriteType, x, y);
			sprite.specialModeCounter = Random.randomInt(FPS * 2);
			sprite.specialMode = mode;
			sprite.canHitTest = true;
			
			scene.otherSprites.add(sprite);
			scene.sprites = null;
		}
	}
	
	function render() {
		grass = ImageLibrary.get('grass');
		y = GROUND_Y;
		while (y < SCREEN_HEIGHT) {
			for (x = 0; x < SCREEN_WIDTH; x += grass.width) {
				grass.draw(x, y);
			}
			y += grass.height;
		}
		
		progress = 1.0 - 1.0 * this.counter / FPS;
		width = floor(progress * SCREEN_WIDTH / 2);
		GFX.Draw.rectangle(0, 0, width, SCREEN_HEIGHT, 255, 255, 255);
		GFX.Draw.rectangle(SCREEN_WIDTH - width, 0, width, SCREEN_HEIGHT, 255, 255, 255);
	}
}