import Math;
import GFX;

enum SpriteType {
	LETTER,
	PERSON,
	TOTORO,
	SMOKE,
	CAT,
	BIRD,
	TREE,
}

class Sprite {
	
	field type;
	field x;
	field y;
	field letterMode;
	field canHitTest = false;
	field specialMode = null;
	field specialModeCounter = 0;
	field isSleeping = false;
	field isLeftFacing = false;
	field dx = 0;
	field dy = 0;
	field isMoving = false;
	field personFlavor = 'person1';
	field personFlavorIndex = 0;
	field letter;
	field lastImageBounds = null;
	field renderOffsetXY = null;
	field homeX;
	field homeY;
	field isFrozen = false;
	field dead = false;
	field lifetime = 0;
	field inversion = 0;
	
	static field PERSON_FLAVORS = 'person1 person2 person3 person4'.split(' ');
	
	static constructor() {
		Sprite.PERSON_FLAVORS.shuffle();
	}
	
	constructor(type, x, y) {
		this.type = type;
		this.x = x;
		this.y = y;
		this.homeX = x;
		this.homeY = y;
	}
	
	function update(scene) {
		switch (this.type) {
			case SpriteType.PERSON:
				this.updatePerson(scene);
				break;
			case SpriteType.LETTER:
				this.updateLetter(scene);
				break;
			case SpriteType.SMOKE:
				this.lifetime++;
				if (this.lifetime > 6) {
					this.dead = true;
				}
				break;
			case SpriteType.BIRD:
				if (this.specialModeCounter++ >= FPS * 2) {
					this.specialModeCounter = -Random.randomInt(FPS);
					this.specialMode = this.specialMode == 'left' ? 'right' : 'left';
				}
				
				if (this.specialMode == 'left') {
					this.dx = -1;
				} else {
					this.dx = 1;
				}
				break;
			case SpriteType.CAT:
				if (this.specialModeCounter++ >= FPS * 2) {
					this.specialMode = ['left', 'right', 'wait'][Random.randomInt(3)];
					this.specialModeCounter = 0;
				}
				switch (this.specialMode) {
					case 'left':
						this.dx = -1;
						break;
					case 'right':
						this.dx = 1;
						break;
					case 'wait':
						break;
				}
				break;
			default: break;
		}
		
		if (this.dx != 0) {
			this.isLeftFacing = this.dx < 0;
		}
		
		this.isMoving = this.dx != 0 || this.dy != 0;
		
		this.x += this.dx;
		this.y += this.dy;
		this.dx = 0;
		this.dy = 0;
	}
	
	static field PERSON_MODES = [
		'left',
		'right',
		'left',
		'right',
		'sleep',
		null,
	];
	
	function updateLetter(scene) {
		
	}
	
	function updatePerson(scene) {
		if (this.isFrozen)
			return;
		
		
		if (this.x < -50) this.x = SCREEN_WIDTH + 45;
		if (this.x > SCREEN_WIDTH + 50) this.x = -45;
		
		if (this.specialMode != null) {
			switch (this.specialMode) {
				case 'left':
					this.dx = -3;
					break;
				
				case 'right':
					this.dx = 3;
					break;
					
				case 'sleep':
					this.isSleeping = true;
					break;
			}
		}
		
		if (this.specialModeCounter++ >= FPS * 2) {
			this.isSleeping = false;
			this.changePersonMode();
		}
	}
	
	function changePersonMode() {
		Sprite.PERSON_MODES.shuffle();
		this.specialModeCounter = 0;
		this.specialMode = Sprite.PERSON_MODES[0];
	}
	
	function render(rc) {
		x = floor(this.x);
		y = floor(this.y);
		
		switch (this.type) {
			case SpriteType.PERSON:
				return this.renderPerson(rc, x, y);
			case SpriteType.LETTER:
				return this.renderLetter(rc, x, y);
			case SpriteType.TOTORO:
				return this.renderTotoro(rc, x, y);
			case SpriteType.SMOKE:
				return this.renderSmoke(rc, x, y);
			case SpriteType.CAT:
				return this.renderCat(rc, x, y);
			case SpriteType.BIRD:
				return this.renderBird(rc, x, y);
			case SpriteType.TREE:
				return this.renderTree(rc, x, y);
		}
	}
	
	function renderCat(rc, x, y) {
		path = 'cat_sitting';
		if (this.isMoving) {
			path = 'cat' + ((rc / 5) % 2 + 1);
		}
		img = ImageLibrary.get(path, this.isLeftFacing);
		left = this.x - img.width / 2;
		top = this.y - img.height;
		img.draw(left, top);
		this.updateBounds(img, left, top);
	}
	
	function renderBird(rc, x, y) {
		img = ImageLibrary.get('bird' + ((rc / 3) % 2 + 1), this.isLeftFacing);
		left = this.x - img.width / 2;
		top = this.y - img.height;
		img.draw(left, top);
		this.updateBounds(img, left, top);
	}
	
	function renderTree(rc, x, y) {
		img = ImageLibrary.get('tree');
		left = this.x - img.width / 2;
		top = this.y - img.height;
		img.draw(left, top);
		this.updateBounds(img, left, top);
	}
	
	function renderSmoke(rc, x, y) {
		img = ImageLibrary.get('smoke' + (this.lifetime < 3 ? '1' : '2'));
		img.draw(this.x - img.width / 2, this.y - img.height + 5);
	}
	
	function renderTotoro(rc, x, y) {
		img = ImageLibrary.get('characters/running_char' + ((rc / 2) % 2 + 1));
		img.draw(this.x - img.width / 2, this.y - img.height);
	}
	
	function renderPerson(rc, x, y) {
		isWalk = this.isMoving && ( (rc / 4) % 2) == 0;
		
		img = ImageLibrary.get('characters/' + this.personFlavor + '_' + (isWalk ? 'stand' : 'walk'), !this.isLeftFacing);
		left = x - img.width / 2;
		top = y - img.height;
		img.draw(left, top);
		this.updateBounds(img, left, top);
	}
	
	function renderLetter(rc, x, y) {
		letter = this.letter.lower();
		if (letter == '&') letter = 'amp';
		left = x - 30;
		top = y - 30;
		
		if (this.renderOffsetXY != null) {
			left += this.renderOffsetXY[0];
			top += this.renderOffsetXY[1];
		}
		
		img = ImageLibrary.get('letters/black/' + letter);
		
		if (this.specialMode != null) {
			switch (this.specialMode) {
				case 'A-run':
					img = ImageLibrary.get('letters/black/a_run_' + ((rc / 4) % 2 + 1));
					break;
				case 'O-bomb':
					oldImg = img;
					img = ImageLibrary.get('letters/black/o_bomb' + (rc % 3 + 1));
					top += oldImg.height - img.height;
					break;
				default:
					break;
			}
		}
		
		img.draw(left, top);
		
		if (this.inversion > 0) {
			img = ImageLibrary.get('letters/white/' + letter);
			img.drawWithAlpha(left, top, this.inversion);
		}
		
		this.updateBounds(img, left, top);
	}
	
	function updateBounds(img, left, top) {
		if (this.canHitTest) {
			if (this.lastImageBounds == null) this.lastImageBounds = [0] * 4;
			this.lastImageBounds[0] = left;
			this.lastImageBounds[1] = top;
			this.lastImageBounds[2] = left + img.width;
			this.lastImageBounds[3] = top + img.height;
		}
	}
}